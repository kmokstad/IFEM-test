set title "Cantilever beam with linear distributed load"
E = 1000.0
nu = 0.3
L = 10.0
h = 1.0
A = 1.0
I = A*h*h/12.0
kappa = 1.2
q0 = 0.02
U(x) = ((2.0*L*L*L-L*L*x+0.1*x*x*x)*x*x + (1.0+nu)*kappa*h*h*(L*L-x*x/3.0)*x) * q0/(12.0*E*I)
set xrange [0:10]
set xlabel "x [m]"
set ylabel "Uz [m]"
set key bottom
plot U(x)
pause 3 "Wait for convergence plots..."

set key top
set xlabel "Number of degrees of freedom"
set ylabel "L2-norm error in displacement [m]"
set logscale xy
set xrange [10:4000]
plot \
'Canq1-L1.dat' using 1:3 with linespoints title "p=1", \
'Canq1-L2.dat' using 1:3 with linespoints title "Lagrange p=2", \
'Canq1-L3.dat' using 1:3 with linespoints title "Lagrange p=3", \
'Canq1-L4.dat' using 1:3 with linespoints title "Lagrange p=4", \
'Canq1-S2.dat' using 1:3 with linespoints title "Splines p=2", \
'Canq1-S3.dat' using 1:3 with linespoints title "Splines p=3", \
'Canq1-S4.dat' using 1:3 with linespoints title "Splines p=4"
pause 3 "Wait for png-output..."
set term png
set output "Canq1.png"
replot
