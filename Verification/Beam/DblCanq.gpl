set title "Double cantilever beam with constant distributed load"
E = 1000.0
nu = 0.3
L = 10.0
h = 1.0
A = 1.0
I = A*h*h/12.0
kappa = 1.2
q = 0.1
U(x) = ((4.0*L*L-4.0*L*x+x*x)*x*x + 4.0*(1.0+nu)*kappa*h*h*(L-0.5*x)*x) * q/(24.0*E*I)
set xrange [0:10]
set xlabel "x [m]"
set ylabel "Uz [m]"
set key bottom
plot U(x)
pause 3 "Wait for convergence plots..."

set key top
set xlabel "Number of degrees of freedom"
set ylabel "L2-norm error in displacement [m]"
set logscale xy
set xrange [10:4000]
plot \
'DblCanq-L1.dat' using 1:3 with linespoints title "p=1", \
'DblCanq-L2.dat' using 1:3 with linespoints title "Lagrange p=2", \
'DblCanq-L3.dat' using 1:3 with linespoints title "Lagrange p=3", \
'DblCanq-S2.dat' using 1:3 with linespoints title "Splines p=2", \
'DblCanq-S3.dat' using 1:3 with linespoints title "Splines p=3"
pause 3 "Wait for png-output..."
set term png
set output "DblCanq.png"
replot
